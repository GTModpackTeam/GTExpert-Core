import com.gtnewhorizons.retrofuturagradle.mcp.ReobfuscatedJar
import org.jetbrains.gradle.ext.Gradle

plugins {
    id('java')
    id('java-library')
    id('maven-publish')
    id('idea')
    id('org.jetbrains.gradle.plugin.idea-ext') version "$idea_ext_version"
    id('com.gtnewhorizons.retrofuturagradle') version "$rfg_version"
    //id('net.darkhax.curseforgegradle') version "$curseforge_gradle_version" apply false
    //id('com.modrinth.minotaur') version "$minotaur_version" apply false
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
    }
}

version = project.mod_version
group = project.maven_group
archivesBaseName = project.archives_base_name

// Set the toolchain version to decouple the Java we run Gradle with from the Java used to compile and run the mod
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(java_version))
        // Azul covers the most platforms for Java 8 toolchains, crucially including MacOS arm64
        vendor.set(JvmVendorSpec.AZUL)
    }
    // Generate sources and javadocs jars when building and publishing
    withSourcesJar()
    // javadoc jar throws errors on incomplete docs
    // docs are also contained in the sources jar, so omit the javadoc-only jar
    //withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

minecraft {
    mcVersion = '1.12.2'
    def args = ["-ea:${project.group}"]
    if (project.use_coremod.toBoolean()) {
        args << '-Dfml.coreMods.load=' + coremod_plugin_class_name
    }
    if (project.use_mixins.toBoolean()) {
        args << '-Dmixin.hotSwap=true'
        args << '-Dmixin.checks.interfaces=true'
        args << '-Dmixin.debug.export=true'
    }
    extraRunJvmArguments.addAll(args)

    useDependencyAccessTransformers = true

    injectedTags.put("VERSION", project.version)
}

// Generate a my.project.Tags class with the version number as a field
tasks.injectTags.configure {
    outputClassName.set("${project.group}.Tags")
}

repositories {
    maven {
        url = 'https://maven.cleanroommc.com'
    }
    maven { url = "https://repo.spongepowered.org/maven" }
    //maven { url "https://maven.mcmoddev.com/" }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { // JEI
        name = 'Progwml6 Maven'
        url = 'https://dvs1.progwml6.com/files/maven'
    }
    maven { // CraftTweaker and JEI Backup
        name = 'BlameJared Maven'
        url = 'https://maven.blamejared.com'
    }
    maven {  // Autoconfig and Autosave
        name = 'Mod Maven'
        url = 'https://modmaven.dev'
    }
    mavenLocal() // Must be last for caching to work
}

dependencies {
    // Hard Dependencies
    // the CCL deobf jar uses very old MCP mappings, making it error at runtime in runClient/runServer
    // therefore we manually deobf the regular jar
    implementation fg.deobf("curse.maven:codechicken-lib-1-8-${ccl_pid}:${ccl_fid}")
    //implementation fg.deobf("curse.maven:gregtech-ce-unofficial-${ceu_pid}:${ceu_fid}")
    // fix NPE with NBTMatcher https://github.com/GregTechCEu/GregTech/pull/1710
    implementation (files("libs/gregtech-1.12.2-2.6.1-beta-deobf.jar"))
    implementation fg.deobf("curse.maven:gregicality-multiblocks-${gcm_pid}:${gcm_fid}")
    implementation fg.deobf("curse.maven:ae2-extended-life-${ae2_pid}:${ae2_fid}")
    implementation fg.deobf("curse.maven:enderio-${eio_pid}:${eio_fid}")
    implementation fg.deobf("curse.maven:ender-io-conduits-${eic_pid}:${eic_fid}")
    implementation fg.deobf("curse.maven:endercore-${eco_pid}:${eco_fid}")
    implementation "info.loenwind.autoconfig:AutoConfig:${minecraft_version}-${autoconfig_version}"
    implementation "info.loenwind.autosave:AutoSave:${minecraft_version}-${autosave_version}"
    implementation rfg.deobf("curse.maven:baubles-${bal_pid}:${bal_fid}")
    implementation rfg.deobf("curse.maven:shadowfacts-forgelin-${sff_pid}:${sff_fid}")
    implementation rfg.deobf("curse.maven:ae-additions-extra-cells-2-fork-${aea_pid}:${aea_fid}")

    // Soft Dependencies
    implementation rfg.deobf("curse.maven:gregtech-food-option-${gfo_pid}:${gfo_fid}")
    implementation rfg.deobf("curse.maven:redstone-flux-${ref_pid}:${ref_fid}")
    implementation rfg.deobf("curse.maven:brandons-core-${brc_pid}:${brc_fid}")
    implementation rfg.deobf("curse.maven:draconic-evolution-${dre_pid}:${dre_fid}")
    implementation rfg.deobf("curse.maven:draconicadditions-${dra_pid}:${dra_fid}")
    implementation "mezz.jei:jei_1.12.2:${jei_version}"
    implementation "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-${crt_version}"
    implementation rfg.deobf("curse.maven:top-${top_pid}:${top_fid}")
    implementation rfg.deobf("curse.maven:ctm-${ctm_pid}:${ctm_fid}")

    // use a local jar for GroovyScript to avoid a bug from Mixin and ForgeGradle,
    // causing crashes at run-time when deobfuscated
    implementation files("libs/groovyscript-0.4.0.jar")
    //implementation rfg.deobf("curse.maven:groovyscript-${grs_pid}:${grs_fid}")

    // Tests
    testImplementation "org.junit.jupiter:junit-jupiter:${junit_version}"
    testImplementation "org.hamcrest:hamcrest:${hamcrest_version}"

    // Compile-Time Dependencies
    compileOnly "org.jetbrains:annotations:${annotations_version}"
    annotationProcessor "org.jetbrains:annotations:${annotations_version}"
    if (project.use_assetmover.toBoolean()) {
        implementation "com.cleanroommc:assetmover:${assetmover_version}"
    }

    // GroovyScript dependency
    implementation "zone.rong:mixinbooter:${mixinbooter_version}"

    // Mixin dependencies
    api("org.spongepowered:mixin:${mixin_version}") { transitive = false }
    annotationProcessor("org.spongepowered:mixin:${mixin_version}") { transitive = false }

    annotationProcessor("org.ow2.asm:asm-debug-all:${asm_debug_version}")
    annotationProcessor("com.google.guava:guava:${guava_version}-jre")
    annotationProcessor("com.google.code.gson:gson:${gson_version}")

    // Boot error fix
    runtimeOnly (files("libs/EnderCore-1.12.2-0.5.76-core.jar"))
}

def mixinConfigRefMap = 'mixins.' + project.maven_group + '.refmap.json'
def mixinTmpDir = buildDir.path + File.separator + 'tmp' + File.separator + 'mixins'
def refMap = "${mixinTmpDir}" + File.separator + mixinConfigRefMap
def mixinSrg = "${mixinTmpDir}" + File.separator + "mixins.srg"

if (project.use_mixins.toBoolean()) {
    tasks.named("reobfJar", ReobfuscatedJar).configure {
        extraSrgFiles.from(mixinSrg)
    }

    tasks.named("compileJava", JavaCompile).configure {
        doFirst {
            new File(mixinTmpDir).mkdirs()
        }
        options.compilerArgs += [
            "-AreobfSrgFile=${tasks.reobfJar.srg.get().asFile}",
            "-AoutSrgFile=${mixinSrg}",
            "-AoutRefMapFile=${refMap}",
        ]
    }
}

if (project.use_access_transformer.toBoolean()) {
    for (File at : sourceSets.getByName("main").resources.files) {
        if (at.name.toLowerCase().endsWith("_at.cfg")) {
            tasks.deobfuscateMergedJarToSrg.accessTransformerFiles.from(at)
            tasks.srgifyBinpatchedJar.accessTransformerFiles.from(at)
        }
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', minecraft_version
    // replace stuff in mcmod.info, nothing else
    filesMatching(['mcmod.info', 'pack.mcmeta']) { fcd ->
        // replace version and mcversion
        fcd.expand (
            'version': project.version,
            'mcversion': minecraft_version
        )
    }

    if (project.use_access_transformer.toBoolean()) {
        rename '(.+_at.cfg)', 'META-INF/$1' // Access Transformers
    }

    if (project.use_mixins.toBoolean()) {
        // Embed mixin refmap
        from refMap
        dependsOn("compileJava")
    }
}

jar {
    manifest {
        def attribute_map = [:]
        if (use_coremod.toBoolean()) {
            attribute_map['FMLCorePlugin'] = coremod_plugin_class_name
            if (include_mod.toBoolean()) {
                attribute_map['FMLCorePluginContainsFMLMod'] = true
                attribute_map['ForceLoadAsMod'] = project.gradle.startParameter.taskNames[0] == "build"
            }
        }
		if (use_access_transformer.toBoolean()) {
            attribute_map['FMLAT'] = maven_group + '_at.cfg'
        }
        attributes(attribute_map)
    }

    // Add all embedded dependencies into the jar
    from provider {
        configurations.embed.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

idea {
    module {
        inheritOutputDirs = true
        downloadJavadoc = true
        downloadSources = true
    }
    project {
        settings {
            runConfigurations {
                "1. Run Client"(Gradle) {
                    taskNames = ["runClient"]
                }
                "2. Run Server"(Gradle) {
                    taskNames = ["runServer"]
                }
                "3. Run Obfuscated Client"(Gradle) {
                    taskNames = ["runObfClient"]
                }
                "4. Run Obfuscated Server"(Gradle) {
                    taskNames = ["runObfServer"]
                }
            }
            compiler.javac {
                afterEvaluate {
                    javacAdditionalOptions = "-encoding utf8"
                    moduleJavacAdditionalOptions = [
                        (project.name + ".main"): tasks.compileJava.options.compilerArgs.collect { '"' + it + '"' }.join(' ')
                    ]
                }
            }
        }
    }
}

// Create API library jar
tasks.register('apiJar', Jar) {
    archiveClassifier.set 'api'
    from(sourceSets.main.java) {
        include maven_group + '/api/**'
    }

    from(sourceSets.main.output) {
        include maven_group + '/api/**'
    }
}

sourceSets {
    test {
        java {
            compileClasspath += patchedMc.output + mcLauncher.output
            runtimeClasspath += patchedMc.output + mcLauncher.output
        }
    }
}

tasks.named("processIdeaSettings").configure {
    dependsOn("injectTags")
}
